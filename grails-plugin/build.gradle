configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if(details.requested.group == 'org.grails' &&
                details.requested.name.startsWith('grails-datastore') &&
                details.requested.name != 'grails-datastore-gorm-hibernate5') {
            details.useVersion(gormVersion)
        }
        if(details.requested.group == 'org.codehaus.groovy' && details.requested.name.startsWith('groovy')) {
            details.useVersion(groovyVersion)   
        }
        if(details.requested.group == 'org.springframework') {
            details.useVersion(springVersion)
        }
    }
}

dependencies {
    documentation ("org.grails:grails-core:$grails3Version")
    documentation ("org.grails:grails-bootstrap:$grails3Version")
    documentation ("org.grails:grails-spring:$grails3Version")

    compileOnly("org.grails:grails-bootstrap:$grails3Version")
    compileOnly("org.grails:grails-core:$grails3Version")
    compileOnly "org.spockframework:spock-core:$spockVersion", {
        exclude module:'groovy-all'
    }

    compile "org.springframework.boot:spring-boot:$springBootVersion"
    compile "org.springframework:spring-orm:$springVersion"
    compile "org.hibernate:hibernate-core:${hibernate5Version}"
    compile "org.grails:grails-datastore-web:$gormVersion"
    compile "org.grails:grails-datastore-gorm-support:$gormVersion"
    compile project(":grails-datastore-gorm-hibernate5"), {
        exclude group:'org.springframework', module:'spring-context'
        exclude group:'org.springframework', module:'spring-core'
        exclude group:'org.springframework', module:'spring-beans'
        exclude group:'org.springframework', module:'spring-tx'
        exclude group:'org.grails', module:'grails-bootstrap'
        exclude group:'org.codehaus.groovy', module:'groovy-all'
        exclude group:'org.grails', module:'grails-core'
        exclude group:'javax.transaction', module:'jta'
    }
    testRuntime "org.yaml:snakeyaml:$snakeyamlVersion"
    testCompile "org.grails:grails-gorm-testing-support:$testingSupportVersion"
    testRuntime "com.h2database:h2:1.4.192"
    testRuntime "org.apache.tomcat:tomcat-jdbc:$tomcatVersion"
    testRuntime "org.apache.tomcat.embed:tomcat-embed-logging-log4j:8.5.2"
    testRuntime "org.springframework:spring-aop:$springVersion"
    testRuntime "org.springframework:spring-expression:$springVersion"
}

groovydoc.classpath += configurations.documentation
apply from: "${rootProject.projectDir}/gradle/testVerbose.gradle"
