To use GORM for Hibernate in Spring Boot add the necessary dependencies to your Boot application:

[source,groovy,subs="attributes"]
----
compile("org.grails:gorm-hibernate5-spring-boot:{version}")
compile "org.hibernate:hibernate-core"
compile "org.hibernate:hibernate-ehcache"
runtime "com.h2database:h2:1.4.192"
// for MySQL
// runtime "mysql:mysql-connector-java"

// for connection pooling
runtime "org.apache.tomcat:tomcat-jdbc:8.5.0"
runtime "org.apache.tomcat.embed:tomcat-embed-logging-log4j:8.5.0"
----

Then ensure you have configured a http://docs.spring.io/spring-boot/docs/current/reference/html/boot-features-sql.html[datasource and Hibernate as per the Spring Boot guide]. For example in the case of MySQL:

[source,yaml]
----
hibernate:
    hbm2ddl:
        auto: update
    dialect: org.hibernate.dialect.MySQL5InnoDBDialect
spring:
    datasource:
        driverClassName: com.mysql.jdbc.Driver
        url:   jdbc:mysql://127.0.0.1:3306/gorm
        username: root
        password: ""
----

TIP: If if you prefer to use the Grails way of configuring the `DataSource` (with `dataSource.url` etc.) then you can add `@EnableAutoConfiguration(exclude = DataSourceAutoConfiguration)` to your `Application` class, which will allow GORM to take over configuring the data source.

Ensure your Boot `Application` class is annotated with `ComponentScan`, for example:

[source,groovy]
----
import org.springframework.boot.SpringApplication
import org.springframework.boot.autoconfigure.EnableAutoConfiguration
import org.springframework.context.annotation.*

@Configuration
@EnableAutoConfiguration
@ComponentScan
class Application {
    static void main(String[] args) {
        SpringApplication.run Application, args
    }
}
----

NOTE: Using `ComponentScan` without a value results in Boot scanning for classes in the same package or any package nested within the `Application` class package.
If your GORM entities are in a different package specify the package name as the value of the `ComponentScan` annotation.

Finally create your GORM entities and ensure they are annotated with `grails.persistence.Entity`:

[source,groovy]
----
import grails.persistence.*

@Entity
class Person {
    String firstName
    String lastName
}
----

Note that Spring Boot does not include any kind of OpenSessionInView interceptor so if you try and invoke GORM methods in a Spring `@Controller` you may encounter a session not found error. To eliminate this problem make sure your `@Controller` methods are annotated with `@Transactional`. For example:

[source,groovy]
----
import org.springframework.transaction.annotation.Transactional
import org.springframework.web.bind.annotation.RequestMapping
import org.springframework.web.bind.annotation.RestController

@RestController
class PersonController {

    @RequestMapping("/people")
    @Transactional(readOnly = true)
    public List<String> people() {
        Person.list().collect { Person p ->
            "$p.firstName $p.lastName".toString()
        }
    }
}

----

In addition, if you wish to return a GORM instance from a Spring `@Controller`, it should be noted that Spring uses Jackson for JSON marshalling, and Jackson will attempt to marshal the entire object to JSON, which can present an issue since GORM adds additional persistence related properties to your domain instance. To resolve this issue you should use `@JsonIgnoreProperties` on your GORM entity class to ignore any properties added by GORM:

[source,groovy]
----
import grails.persistence.*
import com.fasterxml.jackson.annotation.JsonIgnoreProperties

@Entity
@JsonIgnoreProperties(['dirtyPropertyNames', 'errors', 'dirty', 'attached', 'version'])
class Person {
    String firstName
    String lastName
}
----
