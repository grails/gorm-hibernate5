dependencies {
    compile "org.slf4j:slf4j-api:$slf4jVersion"

    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile("org.grails:grails-datastore-gorm:$gormVersion")
    compile "org.springframework:spring-orm:$springVersion"
    compile("org.hibernate:hibernate-core:$hibernate5Version") {
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'com.h2database', module:'h2'
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'org.slf4j', module:'jcl-over-slf4j'
        exclude group:'org.slf4j', module:'slf4j-api'
        exclude group:'org.slf4j', module:'slf4j-log4j12'
        exclude group:'xml-apis', module:'xml-apis'
    }
    compile("org.hibernate:hibernate-validator:$hibernateValidatorVersion") {
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'org.slf4j', module:'slf4j-api'
    }
    compile 'javax.validation:validation-api:2.0.1.Final'
    runtime('javax.el:javax.el-api:3.0.1-b06')
    runtime "javax.xml.bind:jaxb-api:$jaxbVersion"
    runtime "com.sun.xml.bind:jaxb-impl:$jaxbVersion"

    compileOnly ("net.sf.ehcache:ehcache-core:2.4.8") {
        exclude group: 'commons-logging', module:'commons-logging'
    }

    compileOnly ("org.hibernate:hibernate-ehcache:$hibernate5Version") {
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'com.h2database', module:'h2'
        exclude group:'net.sf.ehcache', module:'ehcache'
        exclude group:'net.sf.ehcache', module:'ehcache-core'
        exclude group:'org.hibernate', module:'hibernate-core'
        exclude group:'org.slf4j', module:'jcl-over-slf4j'
        exclude group:'org.slf4j', module:'slf4j-api'
        exclude group:'org.slf4j', module:'slf4j-log4j12'
        exclude group:'xml-apis', module:'xml-apis'
    }

    testCompile "org.codehaus.groovy:groovy-test:$groovyVersion"
    testCompile "org.codehaus.groovy:groovy-sql:$groovyVersion"
    testCompile "org.codehaus.groovy:groovy-json:$groovyVersion"
    testCompile "org.grails:grails-datastore-gorm-tck:$gormVersion"
    testCompile "com.h2database:h2:1.3.164"

    testCompile "net.sf.ehcache:ehcache-core:2.4.6"
    testCompile "org.hibernate:hibernate-ehcache:$hibernate5Version"
    
    testCompile "org.apache.tomcat:tomcat-jdbc:$tomcatVersion"
    testRuntime "org.springframework:spring-aop:$springVersion"
    testRuntime "org.apache.tomcat.embed:tomcat-embed-logging-log4j:$tomcatVersion"

    testRuntime "org.slf4j:slf4j-simple:$slf4jVersion"
    testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    // The groovydoc task needs the Hibernate 4.x jars in the classpath
    documentation("org.hibernate:hibernate-core:${hibernate5Version}")

}

test {
    //TODO: Remove this once https://github.com/grails/grails-data-mapping/issues/1288 is fixed
    exclude '**/BuiltinUniqueConstraintWorksWithTargetProxiesConstraintsSpec.class'
    if (isTravisBuild) {
        maxParallelForks = 1
        forkEvery = 10
    }
    else {
        maxParallelForks = 4
        forkEvery = 20
    }

    jvmArgs = ['-Xmx1028M']
    afterSuite {
        System.out.print('.')
        System.out.flush()
    }
}

test.doFirst {
    def toBaseClassRelativePathWithoutExtension = { String base, String classFile ->
        if (classFile.startsWith(base)) {
            def sansClass = classFile[0 .. classFile.size() - ".class".size() - 1]
            def dollarIndex = sansClass.indexOf('$')
            def baseClass = dollarIndex > 0 ? sansClass[0..dollarIndex - 1] : sansClass
            def relative = baseClass - base - '/'
            relative
        }
        else {
            null
        }
    }    
    def tckClassesFile = project
                            .configurations
                            .testCompile
                            .resolvedConfiguration
                            .getResolvedArtifacts()
                            .find { resolved ->
                                resolved.moduleVersion.id.name == 'grails-datastore-gorm-tck'
                            }.file

    def tckClassesDir = project.file("${project.buildDir}/tck")                            
    copy {
        from zipTree(tckClassesFile)
        into tckClassesDir 
    }
    copy {
        from tckClassesDir
        into sourceSets.test.output.classesDirs.find { it.path.contains('classes' + File.separator + 'groovy') }
        include "**/*.class"
        exclude { details ->
            // Do not copy across any TCK class (or nested classes of that class)
            // If there is a corresponding source file in the particular modules
            // test source tree. Allows a module to override a test/helper.
            if (!details.file.isFile()) {
                return false
            }
            def candidatePath = details.file.absolutePath
            def relativePath = toBaseClassRelativePathWithoutExtension(tckClassesDir.absolutePath, candidatePath)

            if (relativePath == null) {
                throw new IllegalStateException("$candidatePath does not appear to be in the TCK")
            }

            project.file("src/test/groovy/${relativePath}.groovy").exists()
        }
    }
}

apply from: "${rootProject.projectDir}/gradle/testVerbose.gradle"
